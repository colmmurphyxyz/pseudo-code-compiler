// Copyright: (c) 2025, Colm Murphy <colmmurphy016@gmail.com>
// GNU General Public License v3.0+ (see COPYING or https://www.gnu.org/licenses/gpl-3.0.txt)

single_input: _NEWLINE | simple_stmt | compound_stmt _NEWLINE
file_input: (_NEWLINE | stmt)*

funcdef: name "(" [parameters] ")" _NEWLINE function_body

parameters: name ("," name)*

?stmt: simple_stmt | compound_stmt
simple_stmt: small_stmt (";" small_stmt)* [";"] _NEWLINE
?small_stmt: (expr_stmt | assign_stmt | flow_stmt | decl_stmt)
expr_stmt: test
assign_stmt: assign

?decl_stmt: struct_decl_stmt

struct_init_arguments: arith_expr ("," arith_expr)*
    | arith_expr (":" arith_expr)

struct_decl_stmt: single_struct_decl
    | multiple_struct_decl
struct_init: name ["[" struct_init_arguments+ "]"]
single_struct_decl: "let" struct_init "be" "a" "new" name
multiple_struct_decl: "let" struct_init ("and" struct_init)+ "be" "new" name

?flow_stmt: return_stmt | print_stmt | error_stmt | exchange_stmt
return_stmt: "return" test?
print_stmt: "print" test
error_stmt: "error" string
exchange_stmt: ("exchange" | "swap") atom_expr "with" atom_expr

?compound_stmt: if_stmt
    | while_stmt
    | for_stmt
    | repeat_stmt
    | funcdef

if_stmt: "if" test _NEWLINE block_stmt elifs else_?
elifs: elif_*
elif_: "else" "if" test _NEWLINE block_stmt
else_: else_block | else_inline
else_block: "else" _NEWLINE block_stmt
else_inline: "else" simple_stmt block_stmt?
while_stmt: "while" test _NEWLINE block_stmt
for_stmt: for_loop
        | for_iter
for_loop: "for" name "=" test range_op test _NEWLINE block_stmt
for_iter: "for" "each" name~1..2 "in" expr _NEWLINE block_stmt // first 'name' token should be discarded
!range_op: "to"
    | "downto"
repeat_stmt: "repeat" _NEWLINE block_stmt "until" test _NEWLINE

block_stmt: simple_stmt | _INDENT stmt+ _DEDENT

// alias for block_stmt
function_body: _INDENT stmt+ _DEDENT

assign: test "=" test

?test: comparison
    | assign_expr

assign_expr: name "=" test

?comparison: expr (_comp_op expr)*

?expr: or_expr

?or_expr: xor_expr ("or" xor_expr)*
?xor_expr: and_expr ("xor" and_expr)*
?and_expr: shift_expr ("and" shift_expr)*
?shift_expr: arith_expr (_shift_op arith_expr)*
?arith_expr: term (_add_op term)*
?term: factor (_mul_op factor)*
?factor: unary_op factor | power

!unary_op: "+" | "-" | "!"
!_add_op: "+" | "-" | "|"
!_shift_op: "<<" | ">>"
!_mul_op: "*" | "/" | "mod" | "\\\\" | "&"
!_comp_op: "<" | ">" | "==" | ">=" | "<=" | "!=" | "in" | "not" "in" | "is" | "is" "not"
!_power_op: "^" | "**"

?power: atom_expr (_power_op factor)*

?atom_expr: atom_expr "(" [arguments] ")"   -> funccall
            | atom_expr "[" test "]"        -> getitem
            | atom_expr "." name            -> getattr
            | atom

?atom: name         -> var
    | number
    | string
    | "{" [test ("," test)*] "}" -> set_literal
    | "[" test ("," test)* "]" -> array_literal
    | "(" test ")"  -> grouping
    | "NIL"         -> const_nil
    | "TRUE"        -> const_true
    | "FALSE"       -> const_false


arguments: test ("," test)*

number: DEC_INTEGER | DEC_REAL
string: STRING

// other terminals

_NEWLINE: ( /\r?\n[\t ]*/ | COMMENT )+

%ignore /[\t \f]+/ // whitespace
%ignore /\\[\t \f]*\r?\n/ // LINE_CONTINUATION
%declare _INDENT _DEDENT

// more terminals

!name: NAME
// NAME: /[a-zA-Z$][a-zA-Z0-9'\-_^${}\\]*/
NAME: /([a-zA-Z_]([a-zA-Z0-9\-_']|\$[a-zA-Z0-9\-_'^{}\\]+\$)*)|(\$[a-zA-Z0-9\-_'^{}\\]+\$)/
COMMENT: /\/\/[^\n]*/

%import common.ESCAPED_STRING -> STRING

DEC_INTEGER: /[1-9][0-9]*/
    | /0+/
DEC_REAL: /[1-9][0-9]*\.[0-9]+/
    | /0+\.0+/

